plugins {
	id "java"
	id "idea"
	id "jacoco"
	alias libs.plugins.postCompileWeaving
}

dependencies {
	implementation libs.bundles.awsLambda,
			project(":libs:audit-service"),
			project(":libs:cimit-service"),
			project(":libs:common-services"),
			project(":libs:cri-api-service"),
			project(":libs:cri-storing-service"),
			project(':libs:journey-uris'),
			project(":libs:user-identity-service"),
			project(":libs:verifiable-credentials"),
			project(":libs:kms-es256-signer")

	aspect libs.powertoolsLogging,
			libs.powertoolsTracing,
			libs.aspectj

	testImplementation libs.hamcrest,
			libs.junitJupiter,
			libs.mockitoJunit,
			libs.pactConsumerJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:test-helpers')

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	// Configures environment variable to avoid initialization of AWS X-Ray segments for each tests
	environment "LAMBDA_TASK_ROOT", "handler"
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
	exclude 'uk/gov/di/ipv/core/calldcmawasynccri/pact/**'
}

task pactConsumerTests (type: Test) {
	useJUnitPlatform()
	include 'uk/gov/di/ipv/core/calldcmawasynccri/pact/**'
	systemProperties['pact.rootDir'] = "$rootDir/build/pacts"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
