plugins {
	id "java"
	id "idea"
	id "jacoco"
	alias libs.plugins.postCompileWeaving
}

repositories {
	mavenCentral()
}

dependencies {

	implementation platform(libs.awsSdkBom),
			libs.awsSdkDynamodbEnhanced,
			libs.bundles.awsLambda,
			libs.nimbusdsOauth2OidcSdk,
			project(":libs:common-services")

	aspect libs.powertoolsLogging,
			libs.powertoolsTracing,
			libs.aspectj

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	testImplementation libs.junitJupiter,
			libs.mockitoJunit,
			libs.pactProviderJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:pact-test-helpers')

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	// Configures environment variable to avoid initialization of AWS X-Ray segments for each tests
	environment "LAMBDA_TASK_ROOT", "handler"
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
