plugins {
	id "java"
	id "idea"
	id "jacoco"
	id 'io.freefair.aspectj.post-compile-weaving' version '8.4'
}

repositories {
	mavenCentral()
}

dependencies {

	implementation "com.amazonaws:aws-lambda-java-core:$rootProject.ext.dependencyVersions.awsLambdaJavaCore",
			"com.amazonaws:aws-lambda-java-events:$rootProject.ext.dependencyVersions.awsLambdaJavaEvents",
			"com.nimbusds:oauth2-oidc-sdk:$rootProject.ext.dependencyVersions.nimbusdsOauth2OidcSdk",
			"software.amazon.awssdk:dynamodb-enhanced:$rootProject.ext.dependencyVersions.dynamodbEnhanced",
			project(":libs:common-services")

	aspect "software.amazon.lambda:powertools-logging:$rootProject.ext.dependencyVersions.powertoolsLogging",
			"software.amazon.lambda:powertools-tracing:$rootProject.ext.dependencyVersions.powertoolsTracing"
	aspect('org.aspectj:aspectjrt') {
		version {
			strictly '1.9.8'
		}
	}

	compileOnly "org.projectlombok:lombok:$rootProject.ext.dependencyVersions.lombok"

	annotationProcessor "org.projectlombok:lombok:$rootProject.ext.dependencyVersions.lombok"

	testImplementation "org.junit.jupiter:junit-jupiter:5.10.0",
			"org.mockito:mockito-junit-jupiter:5.7.0",
			project(path: ':libs:common-services', configuration: 'tests')
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

test {
	// Configures environment variable to avoid initialization of AWS X-Ray segments for each tests
	environment "LAMBDA_TASK_ROOT", "handler"
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
