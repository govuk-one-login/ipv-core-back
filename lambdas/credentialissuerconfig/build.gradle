plugins {
	id "java"
	id "idea"
	id "jacoco"
	id 'io.freefair.aspectj.post-compile-weaving' version '6.3.0'
}

repositories {
	mavenCentral()
}

dependencies {

	implementation "com.amazonaws:aws-lambda-java-core:$rootProject.ext.dependencyVersions.awsLambdaJavaCore",
			"com.amazonaws:aws-lambda-java-events:$rootProject.ext.dependencyVersions.awsLambdaJavaEvents",
			"org.slf4j:slf4j-simple:$rootProject.ext.dependencyVersions.slf4j",
			project(":lib")

	aspect "software.amazon.lambda:powertools-tracing:$rootProject.ext.dependencyVersions.powertoolsTracing"

	testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0",
			"com.nimbusds:oauth2-oidc-sdk:$rootProject.ext.dependencyVersions.nimbusdsOauth2OidcSdk",
			"org.junit.jupiter:junit-jupiter:5.8.2",
			"org.mockito:mockito-core:4.1.0",
			"org.mockito:mockito-junit-jupiter:4.1.0",
			project(":lib").sourceSets.test.output
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

task buildZip(type: Zip) {
	from compileJava
	from processResources
	destinationDirectory = file("$rootDir/dist")
	into("lib") {
		from configurations.runtimeClasspath
	}
}

test {
	// Configures environment variable to avoid initialization of AWS X-Ray segments for each tests
	environment "LAMBDA_TASK_ROOT", "handler"
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
