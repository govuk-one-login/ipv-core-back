plugins {
	id "java"
	id "idea"
	id "jacoco"
	alias libs.plugins.postCompileWeaving
}

configurations {
	mockitoAgent
}

dependencies {
	implementation libs.bundles.awsLambda,
			project(":libs:audit-service"),
			project(":libs:cimit-service"),
			project(":libs:common-services"),
			project(":libs:cri-response-service"),
			project(":libs:evcs-service"),
			project(":libs:verifiable-credentials")

	compileOnly	libs.lombok
	annotationProcessor libs.lombok

	testImplementation libs.hamcrest,
			libs.junitJupiter,
			libs.mockitoJunit,
			libs.pactConsumerJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:test-helpers')

	constraints {
		// TODO: remove when new version of au.com.dius.pact.consumer:junit5
		// is able to resolve this vulnerability
		testImplementation('io.netty:netty-codec-http2:4.1.124.Final') {
			because "this version patches the vulnerability CVE-2025-55163"
		}

		// TODO: remove when new version of au.com.dius.pact.consumer:junit5
		// is able to resolve this vulnerability
		testImplementation('io.netty:netty-codec-http:4.1.129.Final') {
			because "this version patches the vulnerabilities CVE-2025-67735 and CVE-2025-58056"
		}
	}

	mockitoAgent(libs.mockitoCore) {
		transitive = false
	}

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
