plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
	alias libs.plugins.postCompileWeaving
}

dependencies {
	implementation libs.jacksonDatabind,
			libs.powertoolsLogging,
			libs.powertoolsTracing,
			libs.powertoolsParameters,
			project(":libs:common-services"),
			project(":libs:verifiable-credentials")

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	aspect libs.powertoolsLogging,
			libs.powertoolsTracing,
			libs.aspectj

	testImplementation libs.junitJupiter,
			libs.mockitoJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:evcs-migration'),
			libs.pactConsumerJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:pact-test-helpers')

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	// Configures environment variable to avoid initialization of AWS X-Ray segments for each tests
	environment "LAMBDA_TASK_ROOT", "handler"
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

task pactConsumerTests (type: Test) {
	useJUnitPlatform()
	include 'uk/gov/di/ipv/core/library/pact/**'
	systemProperties['pact.rootDir'] = "$rootDir/build/pacts"
}


jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
