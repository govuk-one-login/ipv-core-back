plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
	id "com.github.spotbugs" version "6.0.7"
}

repositories {
	mavenCentral()
}

dependencies {
	spotbugs "com.github.spotbugs:spotbugs:$rootProject.ext.dependencyVersions.spotbugs"
	spotbugsPlugins("software.amazon.lambda.snapstart:aws-lambda-snapstart-java-rules:$rootProject.ext.dependencyVersions.awsLambdaSnapstartJavaRules")
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
