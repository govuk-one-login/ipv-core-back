plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
}

configurations {
	mockitoAgent
}

dependencies {
	implementation libs.apacheHttpCore,
			libs.bundles.log4j,
			libs.jacksonDatabind,
			project(":libs:common-services")

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	testImplementation libs.junitJupiter,
			libs.assertJ,
			libs.mockitoJunit,
			libs.pactConsumerJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:test-helpers')

	constraints {
		testImplementation('io.netty:netty-codec-http2:4.1.124.Final') {
			because "this version patches the vulnerability CVE-2025-55163"
		}

		testImplementation('io.netty:netty-codec-http:4.1.129.Final') {
			because "this version patches the vulnerabilities CVE-2025-67735 and CVE-2025-58056"
		}
	}

	mockitoAgent(libs.mockitoCore) {
		transitive = false
	}

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

task pactConsumerTests (type: Test) {
	useJUnitPlatform()
	include 'uk/gov/di/ipv/core/library/ais/pact/**'
	systemProperties['pact.rootDir'] = "$rootDir/build/pacts"
	classpath = sourceSets.test.runtimeClasspath
	testClassesDirs = sourceSets.test.output.classesDirs
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}

configurations {
	tests.extendsFrom(testImplementation)
}

tasks.register('jarTest', Jar) {
	from sourceSets.test.output
	archiveClassifier.set('tests')
}
artifacts {
	tests jarTest
}
