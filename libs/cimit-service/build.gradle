plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
}

configurations {
	mockitoAgent
}

dependencies {
	implementation platform(libs.awsSdkBom),
			libs.apacheHttpCore,
			libs.awsSdkUrlConnectionClient,
			libs.jacksonDatabind,
			libs.powertoolsLogging,
			project(":libs:common-services"),
			project(":libs:verifiable-credentials")

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	testImplementation libs.junitJupiter,
			libs.mockitoJunit,
			libs.pactConsumerJunit,
			project(path: ':libs:common-services', configuration: 'tests'),
			project(path: ':libs:test-helpers')

	mockitoAgent(libs.mockitoCore) {
		transitive = false
	}

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

task pactConsumerTests (type: Test) {
	useJUnitPlatform()
	include 'uk/gov/di/ipv/core/library/cimit/pact/**'
	systemProperties['pact.rootDir'] = "$rootDir/build/pacts"
	classpath = sourceSets.test.runtimeClasspath
	testClassesDirs = sourceSets.test.output.classesDirs
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
