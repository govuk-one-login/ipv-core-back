plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
}

dependencies {
	implementation platform(libs.awsSdkBom),
			libs.awsSdkUrlConnectionClient,
			libs.powertoolsLogging,
			libs.powertoolsParameters,
			project(":libs:common-services"),
			project(":libs:gpg45-evaluator"),
			project(":libs:verifiable-credentials")

	api platform(libs.awsSdkBom),
			libs.awsSdkSqs

	testImplementation libs.junitJupiter,
			libs.mockitoJunit,
			project(path: ':libs:common-services', configuration: 'tests')

	testRuntimeOnly libs.junitPlatform

	compileOnly libs.lombok
	annotationProcessor libs.lombok
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
