plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
}

configurations {
	mockitoAgent
}

dependencies {
	implementation libs.bundles.awsLambda,
			libs.jacksonDatabind,
			libs.mockitoJunit,
			libs.pactConsumerJunit,
			libs.powertoolsLogging,
			project(path: ':libs:common-services')

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	testImplementation libs.junitJupiter,
			libs.hamcrest,
			libs.mockitoJunit

	constraints {
		// TODO: remove when new version of au.com.dius.pact.consumer:junit5
		// is able to resolve this vulnerability
		testImplementation('io.netty:netty-codec-http2:4.1.124.Final') {
			because "this version patches the vulnerability CVE-2025-55163"
		}

		testImplementation('io.netty:netty-codec-http:4.1.129.Final') {
			because "this version patches the vulnerabilities CVE-2025-67735 and CVE-2025-58056"
		}
	}

	mockitoAgent(libs.mockitoCore) {
		transitive = false
	}

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
