plugins {
	id 'java-library'
	id "idea"
	id "jacoco"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation libs.bundles.log4j,
			libs.jacksonDatabind,
			project(":libs:common-services"),
			project(":libs:kms-es256-signer"),
			project(":libs:verifiable-credentials")

	compileOnly libs.lombok
	annotationProcessor libs.lombok

	testImplementation libs.junitJupiter,
			libs.mockitoJunit,
			libs.wiremock,
			project(path: ':libs:common-services', configuration: 'tests')

	testRuntimeOnly libs.junitPlatform
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
