plugins {
	// Apply the java-library plugin for API and implementation separation.
	id 'java-library'
	id "idea"
	id "jacoco"
}


repositories {
	// Use Maven Central for resolving dependencies.
	mavenCentral()
}

dependencies {

	implementation "com.amazonaws:aws-java-sdk-dynamodb:$rootProject.ext.dependencyVersions.awsJavaSdkDynamodb",
			"com.amazonaws:aws-java-sdk-lambda:$rootProject.ext.dependencyVersions.awsJavaSdkLambda",
			"com.amazonaws:aws-java-sdk-sqs:$rootProject.ext.dependencyVersions.awsJavaSdkSqs",
			"com.amazonaws:aws-lambda-java-events:$rootProject.ext.dependencyVersions.awsLambdaJavaEvents",
			"com.google.code.gson:gson:$rootProject.ext.dependencyVersions.gson",
			"com.nimbusds:oauth2-oidc-sdk:$rootProject.ext.dependencyVersions.nimbusdsOauth2OidcSdk",
			"software.amazon.awssdk:dynamodb-enhanced:$rootProject.ext.dependencyVersions.dynamodbEnhanced",
			"software.amazon.awssdk:kms:$rootProject.ext.dependencyVersions.kms",
			"software.amazon.lambda:powertools-logging:$rootProject.ext.dependencyVersions.powertoolsLogging",
			"software.amazon.lambda:powertools-parameters:$rootProject.ext.dependencyVersions.powertoolsParameters",
			project(":libs:journey-uris")

	testImplementation "com.github.tomakehurst:wiremock-jre8:2.35.0",
			"org.junit.jupiter:junit-jupiter:5.9.3",
			"org.mockito:mockito-junit-jupiter:5.4.0",
			"uk.org.webcompere:system-stubs-jupiter:2.0.2"
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")

	compileOnly "org.projectlombok:lombok:$rootProject.ext.dependencyVersions.lombok"
	annotationProcessor "org.projectlombok:lombok:$rootProject.ext.dependencyVersions.lombok"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

tasks.withType(Jar).configureEach { Jar jar ->
	jar.preserveFileTimestamps = false
	jar.reproducibleFileOrder = true
}

tasks.named('jar') {
	manifest {
		attributes('Implementation-Title': project.name,
		'Implementation-Version': project.version)
	}
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)
	}
}
