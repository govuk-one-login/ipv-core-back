plugins {
	id "java"
	alias libs.plugins.sonar
	alias libs.plugins.spotless
	alias libs.plugins.pact
}

sonar {
	properties {
		property "sonar.projectKey", "ipv-core-back"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.25.2").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
		toggleOffOn()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

subprojects {
	task allDeps(type: DependencyReportTask) {}
	configurations.all {
		exclude group: 'software.amazon.awssdk', module: 'apache-client'
	}

	tasks.withType(Test) {
		// Configures environment variables to avoid errors when running tests
		environment "LAMBDA_TASK_ROOT", "handler"
		environment 'AWS_EMF_ENVIRONMENT', 'Local'
		environment 'AWS_XRAY_CONTEXT_MISSING', 'IGNORE_ERROR'
		environment 'POWERTOOLS_METRICS_NAMESPACE', 'test'

		// Class data sharing doesn't work with the tests anyway, and this suppresses the warning
		jvmArgs('-Xshare:off')
	}
}

allprojects {
    configurations.configureEach {
        resolutionStrategy {
            // Remove once pact pulls in this or a newer version
            force 'org.apache.tika:tika-core:3.2.3'
        }
    }

	plugins.withType(JavaPlugin).whenPluginAdded {
		dependencies {
			constraints {
				implementation("com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2:2.20.0") {
					because "older versions are incompatible with Jackson > 2.15"
				}
			}
		}
	}
}

pact {
	publish {
		pactDirectory = "$rootDir/build/pacts"
		pactBrokerUrl = "${System.env.PACT_URL}?testSource=${System.env.PACT_BROKER_SOURCE_SECRET_DEV}"
		pactBrokerUsername = "${System.env.PACT_USER}"
		pactBrokerPassword = "${System.env.PACT_PASSWORD}"
		consumerVersion = "${System.env.GIT_SHA}"
		consumerBranch = "${System.env.GIT_BRANCH}"
	}
}
